<!D0CTYPE html>
<html>
    <head>
        <meta charset="utf-8"/>
        <title>WebGL TEST</title>
        <script src="script.js" type="text/javascript"></script>
        <script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/dat-gui/0.7.6/dat.gui.min.js"></script>
        <script src="gui.js" type="text/javascript"></script>

        <script id="vs" type="vertex-shader">#version 300 es
            in vec3 vPos;
            out vec2 fragCoord;
            out vec2 texCoord;
            void main() {
                fragCoord = (vPos.xy + vec2(1., 1.))/2.;
                texCoord = vec2(fragCoord.x, 1. - fragCoord.y);
                gl_Position = vec4(vPos, 1.);
            }
        </script>
        <script id="fs" type="fragment-shader">#version 300 es
            #define MAX_MARCH 128
            #define MAX_DIST 100.
            precision highp float;
            in vec2 fragCoord;
            in vec2 texCoord;
            layout (location = 0) out vec4 fragColor;
            uniform vec2 resolution;
            uniform vec2 omouse;
            uniform vec2 mouse;
            uniform float mousePress;
            uniform float time;
            uniform sampler2D previous;
            uniform sampler2D tex;

            uniform float light_power;
            uniform float zoom;

            #define PI 3.141592653589
            struct Intersect{
                float dist;
                vec3 col;
            };
            // SDF Reference source
            // https://www.iquilezles.org/www/articles/distfunctions/distfunctions.htm
            
            float sdPlane( vec3 p, vec3 n, float h ){
                // n must be normalized
                return dot(p,n) + h;
            }

            float sdHex(vec3 p, vec2 h, float r) {
                vec3 q = abs(p);
                vec2 temp;
                temp.x = max(q.x*1.732050*0.5 + q.z * 0.5, q.z);
                temp.y = q.y;
                return length(max(temp - h, 0.0)) - r;
            }

            float sdCone( vec3 p, vec2 c )
            {
                // c is the sin/cos of the angle
                vec2 q = vec2( length(p.xz), -p.y );
                float d = length(q-c*max(dot(q,c), 0.0));
                return d * ((q.x*c.y-q.y*c.x<0.0)?-1.0:1.0);
            }
            
            Intersect sdPencil(vec3 p, float h){
                float cdist, hdist, close;
                vec3 cP = p - vec3(0,h,0);
                cdist = sdCone( cP, vec2(0.25881904510252,0.96592582628907))- 0.05;
                float lead = step(0.5, cP.y+ 1.);

                hdist = sdHex(p, vec2(0.5, 5.), 0.01);
                close = step(cdist, hdist);
                Intersect i;
                i.col = (1.-max(close, lead))*vec3(0.87, 0.72, 0.53) + max(close, lead)*vec3(0.9,0,0);
                i.dist = max(cdist, hdist);
                return i;
            }

            Intersect hexTiling(vec3 p){
                vec2 rep = vec2(1.7320508*2., 2.)*2.;
                vec3 p1 = p,
                    p2 = p;
                vec2 p1ID, p2ID;
                p1ID = floor(p.xz/rep)- rep*0.5;
                p1.xz = mod(p.xz, rep) - rep*0.5;
                p2ID = floor((p.xz+ rep*0.5)/rep)- rep*0.5;
                p2.xz = mod(p.xz + rep*0.5, rep) - rep*0.5;
                Intersect p1i, p2i, i;
                p1i = sdPencil(p1, sin(length(p1ID+time))*2.);
                p2i = sdPencil(p2, sin(length(p2ID+time))*2.);

                float d = sdPlane(p, vec3(0,1,0), 20.);
                float pd = min(p1i.dist, p2i.dist);
                i.dist =  min(pd, d);
                float pencil_close = step(p1i.dist, p2i.dist);
                float plane_close = step(pd, d);
                
                i.col = mix(vec3(1), mix(p2i.col, p1i.col, pencil_close), plane_close);
                return i;
            }
            Intersect dist_func(vec3 p){

                return hexTiling(p);
            }

            vec3 get_n(vec3 p){
                float ep = 0.0001;
                vec3 epx = vec3(ep, 0., 0.);
                vec3 epy = vec3(0., ep, 0.);
                vec3 epz = vec3(0., 0., ep);
                return normalize(vec3(
                    dist_func(p+epx).dist-dist_func(p-epx).dist,
                    dist_func(p+epy).dist-dist_func(p-epy).dist,
                    dist_func(p+epz).dist-dist_func(p-epz).dist
                ));
            }
            vec2 norCoord(vec2 coord){
                vec2 temp = coord * 2. - 1.;
                return temp*resolution/min(resolution.x,resolution.y);
            }
            void main() {
                float t = time;
                vec2 uv = norCoord(fragCoord);
                vec3 ro = vec3(0, 6., 6.);

                vec3 lightdir = vec3(1.);
                vec3 lookat = vec3(0);
                float z = 1. - zoom * 0.1;
                
                vec3 f = normalize(lookat-ro);
                vec3 vertical = normalize(cross(vec3(0,1,0), f));
                vec3 horizontal = cross(f, vertical);
                vec3 i = ro + f*z + uv.x * vertical + uv.y * horizontal;
                vec3 rdir = normalize(i -ro);

                vec3 ray = ro;
                vec3 col = vec3(1.);
                float dist = 0.;
                Intersect data;
                int march;
                for(march = 0; march < MAX_MARCH; march++){
                    data = dist_func(ray);
                    dist += data.dist;
                    ray = ro + rdir*dist;
                    if(abs(data.dist) < 0.001)break;
                    if(dist>MAX_DIST) {
                        dist = MAX_DIST;
                        march = MAX_MARCH-1;
                        break;
                    }
                }
                if(abs(data.dist) < 0.001){
                    vec3 n = get_n(ray);
                    float c = light_power*dot(-rdir, n)/PI + 0.4;
                    float norm_factor    = ( light_power + 2. ) / ( 2. * PI );
                    float specular = pow( max( dot( reflect(lightdir, n), rdir ), 0.), light_power )* norm_factor;
                    col = data.col*(c + specular);
                }

                float closeness = float(march) / float(MAX_MARCH);
                float AO = 1.-closeness;
                col *= AO;
                float fog = min(1.0, closeness)*1.0;
                vec3  fog2 = vec3(0.01) * dist;
                col -= vec3(0.1)*fog + fog2;

                fragColor.xyz = col;
                fragColor.w = 1.;
            }
        </script>
        <script id="fssave" type="fragment-shader">#version 300 es
            precision highp float;
            in vec2 fragCoord;
            layout (location = 0) out vec4 fragColor;
            uniform sampler2D previous;

            void main() {
                fragColor = texture(previous, fragCoord);
            }
        </script>
        <script id="fsp" type="fragment-shader">#version 300 es
            precision highp float;
            in vec2 fragCoord;
            layout (location = 0) out vec4 fragColor;
            uniform sampler2D previous;

            void main() {
                fragColor = texture(previous, fragCoord);
            }
        </script>
        <style type="text/css">
            body{
                margin : 0px ;
                padding : 0px ;
            }
            body #wrapper{
                width: 100%;
                height: 100%;
                position: fixed;
            }
        </style>
    </head>
    <body>
        <div id="wrapper">
            <canvas id="canvas"></canvas>
        </div>
    </body>
</html>